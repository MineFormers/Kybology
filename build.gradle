buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

apply plugin: 'scala'
apply plugin: 'forge'

repositories {
    mavenCentral()
    maven {
        name = "sonatype-oss-public"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
}

sourceCompatibility = 1.7
targetCompatibility = 1.7
version = "0.1.0"
group = "de.mineformers.kybology"
archivesBaseName = "kybology"

def jarVersion = ""
project.displayVersion = ""
if (System.getenv("BUILD_NUMBER") != null) {
    if(System.getenv("BUILD_NUMBER").isInteger()) {
        jarVersion = ".${System.getenv("BUILD_NUMBER")}"
    }
    project.displayVersion = System.getenv("BUILD_NUMBER")
}

version = "$version$jarVersion"

minecraft {
    version = "1.7.10-10.13.1.1212-new"
    if (file("../run").exists()) {
        runDir = "../run"
    } else {
        runDir = "eclipse"
    }
}

configurations {
    provided
}

//Include provided for compilation
sourceSets.main.compileClasspath += configurations.provided

dependencies {
    if (file('../MFCore/').exists()) {
        compile project('../MFCore')
    }
    provided 'com.nativelibs4java:scalaxy-loops_2.11:0.1.1'
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version
    inputs.property "build", project.displayVersion

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info', 'version.properties'

        // replace version and mcversion
        expand 'version': project.version, 'mcversion': project.minecraft.version, 'build': project.displayVersion
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info', 'version.properties'
    }
}

tasks.withType(ScalaCompile) {
    scalaCompileOptions.optimize = true
    scalaCompileOptions.additionalParameters = ['-Yclosure-elim', '-Yinline']
}

jar {
    classifier = 'universal'
}

// add a source jar
task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'src'
}

// add a javadoc jar
task javadocJar(type: Jar, dependsOn: scaladoc) {
    classifier = 'javadoc'
    from 'build/docs/scaladoc'
}

// because the normal output has been made to be obfuscated
task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'
}

artifacts {
    archives sourceJar
    archives deobfJar
    archives javadocJar
}

uploadArchives {
    if (System.getenv("mavenUrl") != null) {
        repositories {
            logger.info('Publishing to files server')

            mavenDeployer {

                repository(url: System.getenv("mavenUrl")) {
                    authentication(userName: System.getenv("mavenUser"), password: System.getenv("mavenPassword"))
                }

                pom {
                    groupId = project.group
                    version = project.version
                    artifactId = project.archivesBaseName
                    project {
                        name project.archivesBaseName
                        packaging 'jar'
                        description 'Kybology mod'
                        url 'https://github.com/MineFormers/Kybology'

                        scm {
                            url 'https://github.com/MineFormers/Kybology'
                            connection 'scm:git:git://github.com/MineFormers/Kybology.git'
                            developerConnection 'scm:git:git@github.com:MineFormers/Kybology.git'
                        }

                        issueManagement {
                            system 'github'
                            url 'https://github.com/MineFormers/Kybology/issues'
                        }

                        licenses {
                            license {
                                name 'MIT License'
                                url 'http://opensource.org/licenses/MIT'
                                distribution 'repo'
                            }
                        }

                        developers {
                            developer {
                                id 'PaleoCrafter'
                                name 'Paleo Crafter'
                                roles { role 'developer' }
                            }
                        }
                    }
                }
            }
        }
    }
}